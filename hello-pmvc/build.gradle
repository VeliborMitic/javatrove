/*
 * Copyright 2016-2020 Andres Almiray
 *
 * This file is part of Java Trove Examples
 *
 * Java Trove Examples is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * Java Trove Examples is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with Java Trove Examples. If not, see <http://www.gnu.org/licenses/>.
 */

plugins {
    id 'groovy'
    id 'application'
    id 'idea'
    id 'jacoco'
    id 'com.github.ben-manes.versions' version '0.27.0'
    id 'com.github.hierynomus.license' version '0.15.0'
    id 'org.kordamp.gradle.stats'      version '0.2.2'
}

repositories {
    jcenter()
    mavenCentral()
}

dependencyUpdates.resolutionStrategy = {
    componentSelection { rules ->
        rules.all { selection ->
            boolean rejected = ['alpha', 'beta', 'rc', 'cr', 'pr', 'atlassian'].any { qualifier ->
                selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
            }
            if (rejected) {
                selection.reject('Release candidate')
            }
        }
    }
}

dependencies {
    api "org.slf4j:slf4j-api:$slf4jVersion"
    api "com.google.guava:guava:$guavaVersion"
    api("com.google.inject:guice:$guiceVersion") {
        exclude group: 'org.sonatype.sisu.inject', module: 'cglib'
    }
    api 'ru.vyarus:guice-ext-annotations:1.3.0'
    api "org.kordamp.bootstrapfx:bootstrapfx-core:$bootstrapfxVersion"
    api "org.kordamp.ikonli:ikonli-javafx:$ikonliVersion"
    api "org.kordamp.ikonli:ikonli-typicons-pack:$ikonliVersion"
    api "com.miglayout:miglayout-javafx:$miglayoutVersion"

    runtimeOnly "org.slf4j:slf4j-simple:$slf4jVersion"

    testImplementation "junit:junit:$junitVersion"
    testImplementation "org.mockito:mockito-core:$mockitoVersion"
    testImplementation "org.jukito:jukito:$jukitoVersion"
    testImplementation "org.hamcrest:hamcrest-library:$hamcrestVersion"
    testImplementation "org.awaitility:awaitility:$awaitilityVersion"
    testImplementation "org.testfx:testfx-core:$testfxVersion"
    testImplementation "org.testfx:testfx-junit:$testfxVersion"
    testImplementation "pl.pragmatists:JUnitParams:$junitParamsVersion"
    testImplementation "org.assertj:assertj-core:$assertjVersion"
    testImplementation "com.google.truth:truth:$truthVersion"
    testImplementation "org.jgotesting:jgotesting:$jgotestingVersion"
    testImplementation "org.codehaus.groovy:groovy-all:$groovyVersion"
    testImplementation("org.spockframework:spock-guice:$spockVersion") {
        exclude group: 'org.codehaus.groovy', module: 'groovy-all'
    }
    testImplementation('com.athaydes:spock-reports:1.6.1') {
        transitive = false
    }
    // testImplementation "com.tngtech.jgiven:jgiven-junit:$jgivenVersion"
    testImplementation "org.jbehave:jbehave-core:$jbehaveVersion"
}

mainClassName = 'org.kordamp.javatrove.hello.pmvc.Launcher'
compileGroovy.enabled = false

license {
    header = rootProject.file('gradle/HEADER')
    strictCheck = true
    ignoreFailures = true
    mapping {
        java = 'SLASHSTAR_STYLE'
        fxml = 'XML_STYLE'
    }
    ext.year = '2016-2020'
    ext.author = 'Andres Almiray'
    ext.project = 'Java Trove Examples'
}

jacoco {
    toolVersion = '0.8.5'
}

idea {
    project {
        jdkName '13'
        languageLevel '13'
        ipr {
            withXml { provider ->
                def node = provider.asNode()
                node.component.find { it.'@name' == 'VcsDirectoryMappings' }?.mapping[0].'@vcs' = 'Git'

                def compilerConfiguration = node.component.find { it.'@name' == 'CompilerConfiguration' }
                compilerConfiguration.remove(compilerConfiguration.annotationProcessing)
                compilerConfiguration.append(new XmlParser().parseText("""
    <annotationProcessing>
      <profile default="true" name="Default" enabled="true">
        <processorPath useClasspath="true" />
      </profile>
    </annotationProcessing>"""))
            }
        }
    }
}

/*
test {
    systemProperty 'jgiven.report.dir', 'build/reports/jgiven/json'
    systemProperty 'jgiven.report.text', 'false'
}

task jgivenHtml5Report(type: JavaExec) {
    main = 'com.tngtech.jgiven.report.ReportGenerator'
    args '--sourceDir=build/reports/jgiven/json',
        '--targetDir=build/reports/jgiven/html5',
        '--format=html5',
        '--exclude-empty-scenarios=true',
        '--customcss=build/resources/test/jgiven/custom.css',
        '--show-thumbnails=true'

    classpath = configurations.testCompile
}
test.finalizedBy(jgivenHtml5Report)
*/